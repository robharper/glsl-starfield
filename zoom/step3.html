<!DOCTYPE html>
<html>
<head>
  <title>Radial - Step 1</title>
  <link rel="stylesheet" type="text/css" href="../common/style.css">

  <script id="vertex-shader" type="x-shader/x-vertex">
    attribute vec3 a_pos;
    uniform vec2 u_screensize;
    void main() {
      gl_Position = vec4(a_pos.xy * u_screensize.x, 0.0, 1.0);
    }
  </script>

  <script id="fragment-shader" type="x-shader/x-fragment">
    #ifdef GL_ES
      precision highp float;
    #endif
    uniform vec2 u_screensize;
    uniform float u_time;

    #define STAR_QUANTIZE 4.0
    #define STAR_SPARSENESS 0.99
    #define SPEED 0.15
    #define STAR_FALLOFF 25.

    float random1(vec2 x) {
      return fract(sin(dot(x.xy / 1000., vec2(12.9898, 78.233))) * 43758.5453123);
    }

    void main() {
      // Normalize center of screen to (0, 0)
      vec2 st = (gl_FragCoord.xy - u_screensize/2.0) / STAR_QUANTIZE;

      vec3 color = vec3(0.);
      float t = u_time * SPEED;

      for (float layer=0.0; layer<1.; layer+=0.1) {
        float zoom = 1. - fract(t + layer);
        // Find our pixel's zoomed location and quantize to a 1x1 grid
        vec2 zoomedSt = st * zoom;

        // Quantize pixels to create stars bigger than pixels
        vec2 gridCoord = floor(zoomedSt + 0.5);
        float error = distance(zoomedSt, gridCoord);

        // Offset each layer by a different amount
        float offset = mod((floor(t + layer) + layer), 37.) * 1296.98;

        // Generate noise value in [0,1] for our quantized location
        float noise = random1(gridCoord + offset);

        // Stars exist where random noise number is > STAR_SPARSENESS
        float starIntensity = smoothstep(STAR_SPARSENESS, 1.0, noise);

        // Render stars as points of light that fall off with distance from their center
        float pixelValue = starIntensity / (STAR_FALLOFF * error) * smoothstep(0.6, 0.3, error);

        // Fade stars in from 1 --> 0.3
        float fade = smoothstep(1.0, 0.3, zoom);

        color += pixelValue;
      }

      gl_FragColor = vec4(color,1.0);
    }
  </script>
</head>
<body>
  <canvas id="canvas"></canvas>
  <script>
    const SCALE_FACTOR = 1.0;
  </script>
  <script src="../common/setup.js"></script>
</body>
</html>